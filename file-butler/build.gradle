plugins {
    id 'kotlin'
    id 'kotlin-kapt'
    id "maven-publish"
    id "signing"
}

task javadocJar(type: Jar) {
    archiveClassifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}


dependencies {
    implementation(Deps.core.kotlin)
    implementation(Deps.utils.gson)
    implementation(Deps.utils.moshi)
    kapt(Deps.utils.moshCodeGen)

    testImplementation(Deps.testing.truth)
    testImplementation(Deps.testing.jUnit)
    kaptTest(Deps.utils.moshCodeGen)
    dokkaHtmlPlugin(Deps.utils.dokkaPlugin)
}
sourceSets {
    main.kotlin.srcDirs += 'src/main/kotlin'
    main.java.srcDirs += 'src/main/java'
}

artifacts {
    archives javadocJar, sourcesJar
}

group = "dev.thiagosouto"
version = "0.1.6-SNAPSHOT"


publishing {
    publications {
        mavenJava(MavenPublication) {
            pom {
                name = "file-butler"
                description = "A library to support TDD practice "
                url = "https://github.com/othiagosouto/test-butler"

                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
                developers {
                    developer {
                        id= "othiagosouto"
                        name = "Thiago Souto"
                        email = "thiagosouto@duck.com"
                    }
                }
                scm {
                    connection = "scm:git:git://github.com/othiagosouto/test-butler.git/"
                    developerConnection = "scm:git:ssh://github.com:othiagosouto/test-butler.git"
                    url = "https://github.com/othiagosouto/test-butler"
                }
            }
        }
    }

    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials{
                username = System.getenv("ORG_GRADLE_PROJECT_sonatypeUsername")
                password = System.getenv("ORG_GRADLE_PROJECT_sonatypePassword")
            }
        }
    }
}

signing {
    def signingKey = findProperty("signingKey")
    def signingPassword = findProperty("signingPassword")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.mavenJava
}