plugins {
    id 'java-library'
    id 'kotlin'
    id 'kotlin-kapt'
    id "org.jetbrains.dokka"
    id "maven"
    id "signing"
}

task javadocJar(type: Jar) {
    archiveClassifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

dependencies {
    implementation(Deps.core.kotlin)
    implementation(Deps.utils.gson)
    implementation(Deps.utils.moshi)
    kapt(Deps.utils.moshCodeGen)

    testImplementation(Deps.testing.truth)
    testImplementation(Deps.testing.jUnit)
    kaptTest(moshCodeGen)
    dokkaHtmlPlugin(Deps.utils.dokka)
}
sourceSets {
    main.kotlin.srcDirs += 'src/main/kotlin'
    main.java.srcDirs += 'src/main/java'
}

signing {
    sign configurations.archives
}

artifacts {
    archives javadocJar, sourcesJar
}

group = GROUP
version = VERSION_NAME

ossrhUsername=System.env.SONATYPE_NEXUS_USERNAME
ossrhPassword=System.env.SONATYPE_NEXUS_PASSWORD

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'file-butler'
                packaging 'jar'
                description 'A library to help apply tdd through help functions'
                url 'https://github.com/othiagosouto/test-butler'

                scm {
                    connection 'scm:git:git://github.com/othiagosouto/test-butler.git/'
                    developerConnection 'scm:git:ssh://github.com:othiagosouto/test-butler.git'
                    url 'https://github.com/othiagosouto/test-butler'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'othiagosouto'
                        name 'Thiago Souto silva de barros Santos'
                        email 'soutosss@gmail.com'
                    }
                }
            }
        }
    }
}
